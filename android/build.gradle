buildscript {
    repositories {
        jcenter()
        maven {
            url "https://raw.githubusercontent.com/konikvranik/m2repo/master/"
        }

    }
    dependencies {
        //classpath 'com.android.tools.build:gradle:1.2.3'
        classpath 'com.android.tools.build:gradle:1.3.1'
		classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
		classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
        	classpath 'com.github.triplet.gradle:play-publisher:1.1.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'jacoco-android'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.github.triplet.play'

repositories {
    jcenter()
    mavenLocal()
    maven {
        url "http://pandora.comerge.net:8081/nexus/content/repositories/public_snapshots/"
    }

}
play {
    jsonFile = file('keys.json')
}
if (project.rootProject.file('local.properties').exists()) {
    Properties buildProps = new Properties()
    buildProps.load(project.rootProject.file('local.properties').newDataInputStream())
    buildProps.each { prop ->
        project.ext.set(prop.key, prop.value)
    }
}
def key = System.getenv('SIGNINIG_KEYSTORE_PASSWORD')
if (key != null) {
	project.ext.set('pwd.keystore', key)
}
key = System.getenv('SIGNINIG_KEY_PASSWORD')
if (key != null) {
	project.ext.set('pwd.key', key)
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "net.suteren.worksaldo.android"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 2
        versionName "1.0-beta"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

	def boolean sign = project.ext.has('pwd.keystore')&&project.ext.has('pwd.key')&&project.rootProject.file('local.properties').exists()
    signingConfigs {
        release {
			if(sign){
				storeFile file("release.keystore")
				keyAlias "worksaldo"
				
				storePassword project.ext.get('pwd.keystore')
				keyPassword project.ext.get('pwd.key')
			}
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		if(sign){
			signingConfig signingConfigs.release
		}
	}
	debug {
      		testCoverageEnabled true
    	}
    }

    packagingOptions {
        exclude 'META-INF/jersey-module-version'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
    }

    lintOptions {
        //disable 'InvalidPackage'
    }

    dexOptions {
        preDexLibraries = false
    }
}


project.tasks.withType(com.android.build.gradle.tasks.Dex) {
    additionalParameters = ['--core-library']
}

dependencies {
    compile project(':common')
    compile project(':androidsvg')
    compile(project(':jtoggl')) {
        exclude module: 'junit'
        exclude module: 'javax.inject'
        exclude module: 'jaxb-api'
    }
    compile files('libs/android-ws-combine.jar')
    compile('com.google.inject:guice:3.0') {
        exclude module: 'aopalliance'
    }
    compile('org.simpleframework:simple-xml:2.7.1') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }

    compile 'com.android.support:support-v4:23.1.1'

    testCompile 'junit:junit:4.12'

}

coveralls {
    jacocoReportPath = "${buildDir}/reports/coverage/debug/report.xml"
}

tasks.coveralls {
    dependsOn 'connectedAndroidTest'
    onlyIf { System.env.'CI' }
}
